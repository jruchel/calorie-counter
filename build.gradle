plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.0'
	id 'io.spring.dependency-management' version '1.1.0'
	id "com.diffplug.spotless" version "6.11.0"
}

group = 'com.jruchel'
version = '0.0.1'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

spotless {
	format 'misc', {
		target '**/*.gradle', '**/.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}

	java {
		importOrder 'java', 'javax', 'org', 'com', 'com.timematters.', ''
		removeUnusedImports()
		googleJavaFormat('1.15.0').aosp()
	}
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	//Flapdoodle
	implementation group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo', version: '4.0.7-beta'


	//Mapstruct
	implementation "org.mapstruct:mapstruct:1.5.3.Final"
	annotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"

	//Lombok
	compileOnly "org.projectlombok:lombok:1.18.24"
	annotationProcessor "org.projectlombok:lombok:1.18.24"
	testCompileOnly "org.projectlombok:lombok:1.18.24"
	testAnnotationProcessor "org.projectlombok:lombok:1.18.24"

	//Mapstruct - Lombok binding
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
	testAnnotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"


}

compileJava {
	options.compilerArgs = [
			'-Amapstruct.defaultComponentModel=spring'
	]
}

tasks.named('test') {
	useJUnitPlatform()
}
